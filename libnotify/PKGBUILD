# $Id: PKGBUILD 99716 2010-11-16 22:24:02Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libnotify
pkgver=0.7.1
_pkgve=${pkgver%.*}
pkgrel=1
pkgdesc="Gnome desktop notification library"
arch=('i686' 'x86_64')
url="http://library.gnome.org/devel/libnotify/"
license=('LGPL2.1')
groups=('ypb' 'extra-ypb')
depends=('dbus-glib>=0.92' 'gtk3>=2.99.2' 'glib')
makedepends=('gnome-common')
#provides=('libnotify=${pkgver}')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${_pkgve}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('84dc2a77defb25521d2f1cb7ac34b4d23b55254ba361442658430efb928536c8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2 \
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
