# vim:set ts=2 sw=2 et:
# Maintainer: Michal Kowalski <kowalski TOD michal TA gmail TOD com>

_debplv=8

pkgname=achilles
pkgver=2
pkgrel=2
pkgdesc="An artificial life and evolution simulator that uses Hebbian neural networks and OpenGL/SDL to simulate life in a simplified environment. It is based on Larry Yaeger's PolyWorld idea"
arch=('i686')
# originally there, still has 1st version's tarball, do not remove
#url="http://achilles.sourceforge.net/"
url="http://packages.debian.org/achilles"
# resolves to
#url="http://packages.debian.org/search?keywords=achilles"
# really but looks better in -Qi and should be the stable one
license=('GPL2')
groups=('ypb')
depends=('libgl' 'sdl' 'libpng' 'zlib')
makedepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=
# see url comment above...
#source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
source=(http://ftp.debian.org/debian/pool/main/a/$pkgname/${pkgname}_${pkgver}.orig.tar.gz
        http://ftp.debian.org/debian/pool/main/a/$pkgname/${pkgname}_${pkgver}-${_debplv}.diff.gz)
md5sums=('4514b3f93b8bf790738822da3a0f37de'
         'a74f6bfefcacd10b7b70f7d23487dc16')
noextract=()

_docs='README NEWS ChangeLog AUTHORS'

build() {
  cd "$srcdir/$pkgname-$pkgver"

  gunzip -c ../${pkgname}_${pkgver}-${_debplv}.diff.gz | patch -p1
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  for d in usr/share/{doc/$pkgname-$pkgver,pixmaps,applications,man/man1} ; do
	install -m755 -d $pkgdir/$d ;
  done
  install -m644 debian/$pkgname.1 $pkgdir/usr/share/man/man1
  install -m644 debian/$pkgname.desktop $pkgdir/usr/share/applications
  install -m644 debian/$pkgname.svg $pkgdir/usr/share/pixmaps
  install -m644 -t $pkgdir/usr/share/doc/$pkgname-$pkgver ${_docs}
}

