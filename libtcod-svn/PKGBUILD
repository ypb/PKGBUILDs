# Maintainer: Kevin Whitaker <eyecreate at gmail dot com>
# Contributor: Michal Kowalski <kowalski.michal@gmail.com>

pkgname=libtcod-svn
pkgver=494
pkgrel=1
pkgdesc="Roguelike graphics/utility library"
arch=('i686' 'x86_64')
url="http://doryen.eptalys.net/libtcod/"
license=('BSD')
depends=('libpng' 'sdl' 'zlib' 'libgl')
makedepends=('cmake' 'subversion' 'upx')
conflicts=('libtcod')
provides=('libtcod')
source=('png.patch'
	'cmake.patch')

_svntrunk=http://doryen.eptalys.net/svn-libtcod/trunk
_svnmod=libtcod
_makedir=cmake/Release

build() {
  cd $srcdir  
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  rm -fr $_svnmod-build
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  patch -p0 < ../png.patch
  patch -p0 < ../cmake.patch

  mkdir -p $_makedir
  cd $_makedir
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -G "Unix Makefiles" $srcdir/$_svnmod-build
  make
}

package() {
  cd $_svnmod-build
  cd $_makedir
  make DESTDIR=$pkgdir install
}

md5sums=('fd3417b535ad0dbf91eeffd42202b81e'
         '9968e42f7f7e06d333fadb0e134eda00')
