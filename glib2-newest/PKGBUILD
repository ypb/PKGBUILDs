# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: <SpeedVin at archlinux dot us>
# Based on [testing]'s glib2

pkgname=glib2-newest
pkgver=2.27.93
_majorver=(`echo ${pkgver} | egrep -o '[0-9]+\.[0-9]*+'`)
pkgrel=1
pkgdesc="Common C routines used by GTK+ 2.4 and other libs - Bleeding edge version"
url="http://www.gtk.org/"
arch=(i686 x86_64)
license=('LGPL')
depends=('pcre>=8.02')
makedepends=('python2' 'glib2')
conflicts=('glib2')
provides=("glib2=${pkgver}")
options=('!libtool' '!docs')
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${_majorver}/glib-${pkgver}.tar.bz2
        glib2.sh
        glib2.csh)
sha256sums=(`wget ${source/.t*}.sha256sum -qO - | grep tar.bz2 | cut -d " " -f1`
            '9456872cdedcc639fb679448d74b85b0facf81033e27157d2861b991823b5a2a'
            '8d5626ffa361304ad3696493c0ef041d0ab10c857f6ef32116b3e2878ecf89e3')

build() {
  cd glib-${pkgver}
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam
  make
}

package() {
  cd glib-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm755 ../glib2.sh "${pkgdir}/etc/profile.d/glib2.sh"
  install -m755 ../glib2.csh "${pkgdir}/etc/profile.d/glib2.csh"
  for _i in "${pkgdir}/etc/bash_completion.d/"*; do
      chmod -x "${_i}"
  done
}