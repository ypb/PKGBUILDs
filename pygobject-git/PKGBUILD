# Contributor: Jan de Groot <jgc@archlinux.org>
# Adapted from ABS package
# Maintainer: Jon Nordby <jononor@gmail.com>

_basename=pygobject
pkgname=$_basename-git
pkgver=20110207
_topver=2.27.0
pkgrel=1
pkgdesc="Python bindings for GObject - git version"
arch=('i686' 'x86_64')
license=('LGPL')
groups=('ypb' 'extr-ypb' 'aur-ypb')
depends=('python2' 'glib2>=2.27.5' 'python2-cairo>=1.8.10' 'gobject-introspection>=0.10.2')
provides=("${_basename}=${_topver}")
conflicts=("${_basename}")
options=('!libtool')
url="http://live.gnome.org/PyGObject"
source=(0001-Build-Accept-libtool-2.4-as-well.patch)
md5sums=('a56989b060909129f1ce71361e5b0e42')

_gitroot="git://git.gnome.org/pygobject"
_gitname="${_basename}.origin"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -p1 < "$srcdir"/0001-Build-Accept-libtool-2.4-as-well.patch

  # BUILD
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install

  find "$pkgdir"/usr/share/pygobject -name '*.py' | \
    xargs sed -i "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"
}

