# $Id: PKGBUILD 89866 2010-09-04 14:37:32Z remy $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Michal Kowalski <kowalski TOD michal TA gmail TOD com>

pkgname=tdb
pkgver=1.2.9
pkgrel=1
pkgdesc="A Trivial Database, GDBM/BDB-like, but for simultaneous writers and transactions"
arch=(i686 x86_64)
license=('LGPL3')
groups=('ypb' 'extra-ypb')
url="http://tdb.samba.org/"
source=(http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('4d97d18bbe9040038d4e003495bb8cd1')
makedepends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('python2: for python bindings')
options=()

_waf=buildtools/bin/waf
_docs=(README tracing.txt)

build() {
   cd ${srcdir}/${pkgname}-${pkgver}

   PYTHON=/usr/bin/python2 \
	 XSLTPROC="xsltproc --nonet --catalogs" \
	 python2 ${_waf} configure \
	  --prefix=/usr \
	  --localstatedir=/var \
	  --sysconfdir=/etc/samba

   python2 ${_waf} build
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}

   python2 ${_waf} install --destdir=$pkgdir/

   install -d ${pkgdir}/usr/share/doc/${pkgname}
   for f in ${_docs[*]} ; do
	 install -m644 docs/$f ${pkgdir}/usr/share/doc/${pkgname}/$f
   done
}
