# Maintainer: Mark Pustjens <pustjens@dds.nl>
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: Michal Kowalski <kowalski.michal@gmail.com>

pkgbase=jockey
pkgname=('jockey-common' 'jockey-gtk' 'jockey-kde')
pkgver=0.5.10
pkgrel=3
pkgdesc="Infrastructure and the user interface for finding and installing third-party drivers applicable to the computer"
url="https://launchpad.net/jockey"
arch=('any')
license=('GPL')
groups=('jockey')
source=("http://launchpad.net/${pkgbase}/trunk/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz" "jockey-common.logrotate")
# depends=('dbus-python' 'pyxdg' 'x-kit' 'polkit' 'polkit-gnome' 'consolekit' 'packagekit=0.6.7-2' 'pygtk' 'python-notify' 'lsb-release' 'kdebindings-python' 'pyqt')
makedepends=('python-distutils-extra' 'kdebindings-python' 'pyqt')
options=(!emptydirs)

sha1sums=('3d03565f7a1e6e36fb9aeb40a27262a843eed43a' '7292f1694d23d716251ef9773b11a63c7fa9886e')

_prefix="/usr"
_kde_files='bin/*-kde share/applications/*-kde.* share/autostart/*-kde.* lib/python*/*/jockey/kdeui share/kde4'
_gtk_files='bin/*-gtk share/applications/*-gtk.* share/autostart/*-gtk.* share/jockey/*.ui share/dbus-1/services'
_common_files='../etc bin/*-text share/dbus-1/system-services share/doc/jockey share/jockey/jockey-backend share/locale share/polkit-1 share/icons lib/python*/*/jockey/*.py* lib/python*/*/*.egg-info'

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	# TODO: case on first two with patch not to build kde stuff...
	python setup.py build || return 1
}

_mkcommon() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	python setup.py install --prefix=${_prefix} --root="${pkgdir}" || return 1
}

_clean_out() {
	for pat in "$@" ; do
	    rm -rf ${pkgdir}${_prefix}/${pat}
	done
}

package_jockey-common() {
	pkgdesc="${pkgdesc}. These are common files required by GTK or KDE version of Jockey"
	depends=('dbus-python' 'pyxdg' 'x-kit' 'polkit' 'packagekit=0.6.7-2' 'lsb-release')
	install=${pkgbase}-common.install
	provides=('jockey-common' 'jockey-text')

	_mkcommon || return 1

	_clean_out ${_kde_files} ${_gtk_files}

	mkdir -p ${pkgdir}/etc/logrotate.d
	cp ${srcdir}/jockey-common.logrotate ${pkgdir}/etc/logrotate.d/jockey
}

package_jockey-gtk() {
	pkgdesc="${pkgdesc}. This is Jockey's GTK frontend"
	depends=('jockey-common='$pkgver-$pkgrel 'polkit-gnome' 'consolekit' 'pygtk' 'python-notify')
	optdepends=('gdm: for "out of the box" polkit-gnome/consolekit handling')
	provides=('jockey' 'jockey-gtk')

	_mkcommon || return 1

	_clean_out ${_kde_files} ${_common_files}
}

package_jockey-kde() {
	pkgdesc="${pkgdesc}. This is Jockey's KDE frontend"
	depends=('jockey-common='$pkgver-$pkgrel 'polkit-gnome' 'consolekit' 'kdebindings-python' 'pyqt')
	optdepends=('gdm: for "out of the box" polkit-gnome/consolekit handling')
	provides=('jockey' 'jockey-kde')

	_mkcommon || return 1

	_clean_out ${_gtk_files} ${_common_files}
}
