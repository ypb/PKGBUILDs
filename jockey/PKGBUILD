# Maintainer: Mark Pustjens <pustjens@dds.nl>
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: Michal Kowalski <kowalski.michal@gmail.com>

pkgname=jockey
pkgver=0.5.9
pkgrel=2
pkgdesc="Jockey provides the infrastructure and the user interface for finding and installing third-party drivers which are applicable to the computer"
url="http://launchpad.net/jockey"
arch=('any')
license=('GPL')
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
depends=('dbus-python' 'pyxdg' 'x-kit' 'polkit' 'packagekit=0.6.7-2' 'pygtk' 'python-notify' 'lsb-release')
makedepends=('python-distutils-extra')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	rm -rf kde
	sed -i "s|, 'kde/jockey-kde'||" setup.py
	python setup.py build || return 1
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python setup.py install --prefix="/usr" --root="${pkgdir}" || return 1

	rm -rf "${pkgdir}/usr/share/omf"
	find "${pkgdir}/usr/share/icons" -name "jockey-kde*" -exec rm -f {} \;
	# this one should prolly go into postinstall script...
	ln -s "jockey-gtk" "${pkgdir}/usr/bin/jockey"
}

md5sums=('60ab79e6088f80363dca2749fa79b0bf')
