# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: <david@pako.st>

pkgname=chicken
pkgver=4.6.0
pkgrel=2
pkgdesc='A compiler for R5RS scheme, featuring handy extensions'
arch=('i686' 'x86_64')
url='http://www.call-with-current-continuation.org/'
license=('BSD')
depends=('gcc')
source=("http://code.call-cc.org/releases/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('538a93e786e550ad848a040bcd902184')

build() {
  cd ${pkgname}-${pkgver}

  # parallel builds are not supported..
  export MAKEFLAGS="${MAKEFLAGS/-j?/}"

  # some yummy make options there. check out README in the tarball.
  make PLATFORM=linux PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}

  make PLATFORM=linux PREFIX=/usr DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # should be a symbolic link not a copy otherwise ldconfig will cry
  rm -f ${pkgdir}/usr/lib/libchicken.so
  ln -s /usr/lib/libchicken.so.5 ${pkgdir}/usr/lib/libchicken.so
}
