# Maintainer: xduugu 
# Contributor: Ronald van Haren <ronald.archlinux.org> 
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.4.8
pkgrel=1
pkgdesc="A tiling window manager initialy based on a dwm code rewriting."
arch=('i686' 'x86_64')
url="http://awesome.naquadah.org/"
source=($url/download/$pkgname-$pkgver.tar.xz
	'awesome.desktop')
install=awesome.install
license=('GPL2')
depends=('cairo-xcb' 'pango' 'imlib2' 'lua' 'libev' 'dbus' 'libxdg-basedir' 'startup-notification')
makedepends=('docbook-xsl' 'asciidoc' 'xmlto' 'doxygen' 'gperf' 'cmake' 'luadoc' 'imagemagick')
optdepends=('rlwrap: readline support for awesome-client')
options=('!libtool' 'docs')
md5sums=('6c83543702afa0b8833729a35c69479a'
         '2763cab6a20d4b0f2676329d57ed3a45')
sha256sums=('e878b98abfc10c5fbe1f9d831cc9274a5a36e81719b25aee3f37c809309a2dd7'
            '5c5437448cc9f01be6ccbb298f5c86d0f8c4bcae23a22e6af699aff0d10f642f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make CMAKE_ARGS=" -DPREFIX=/usr -DSYSCONFDIR=/etc \
	-DCMAKE_BUILD_TYPE=RELEASE"
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

 # install desktop file so you can start awesome from your login manager
 install -Dm644 ${srcdir}/awesome.desktop \
        ${pkgdir}/usr/share/xsessions/awesome.desktop
}
