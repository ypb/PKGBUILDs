# Contributor: onestep_ua <onestep@ukr.net>

pkgname=packagekit
pkgver=0.6.7
pkgrel=2
pkgdesc="A system designed to make installing and updating software on your computer easier"
arch=('i686' 'x86_64')
url="http://packagekit.org/"
license=('GPL')
depends=('dbus-glib>=0.82' 'polkit>=0.96' 'networkmanager>=0.8' 'pacman>=3.4.0' 'libtar' 'python>=2.6.5' 'sqlite3' )
makedepends=('intltool' 'pm-utils' 'gir-repository>=0.6.6' 'pacman-glib>=3.4.0')
optdepends=('pacman-glib: for pacman backend')
options=('!libtool')
source=(http://www.packagekit.org/releases/PackageKit-${pkgver}.tar.bz2)
md5sums=('842d6b2b02baabc3b00ee7a77887241a')

build() {
  cd "${srcdir}/PackageKit-${pkgver}"

  # disable building of test backend
  sed -i 's/SUBDIRS = test/SUBDIRS =/' backends/Makefile.in

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/PackageKit \
      --disable-static \
      --disable-gtk-doc \
      --disable-qt \
      --disable-managed \
      --disable-tests \
      --disable-local \
      --disable-browser-plugin \
      --disable-gstreamer-plugin \
      --disable-gtk-module \
      --disable-command-not-found \
      --disable-cron \
      --disable-debuginfo-install \
      --enable-pm-utils \
      --disable-dummy \
      --enable-alpm \
      --enable-pacman \
      --with-default-backend=alpm
  make
}

package() {
  cd "${srcdir}/PackageKit-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # clean up unneeded stuff
  rm -rf "${pkgdir}/usr/share/PackageKit/website"
  rmdir "${pkgdir}/var/log"

  # remove empty transactions db
  rm -f "${pkgdir}/var/lib/PackageKit/transactions.db"

  # rename bash completion script
  mv "${pkgdir}/etc/bash_completion.d/pk-completion.bash" "${pkgdir}/etc/bash_completion.d/pkcon"
}

# vim:set ts=2 sw=2 et:
